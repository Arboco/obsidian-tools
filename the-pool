#! /usr/bin/env fish

function help_function
    echo "By default selection from all pool files."

    echo "Options:"
    echo "  -h       help"
    echo "  -t       Filter selection based on a tag"
    echo "  -i       History, selection decending from the most recent file"
    echo "  -r       Random selection. Works with -t"
    echo "  -u       Select unrated files. Works with -t and/or -r"
end

argparse --name=pool t/tag i/history r/random u/unrated h/help 'n/name=' -- $argv
or return

set script_dir (realpath (status dirname))
set obsidian_folder (ot_config_grab "ObsidianMainFolder")
set notes (ot_config_grab "NotesFolder")
set default_pool_array (rg -l "\- pool" $obsidian_folder)

if set -q _flag_h
    help_function
    exit
end

if set -q _flag_t
    set chosen_tag (rg -o --no-filename "  - .*" $default_pool_array | sed 's/[ -]//g' | sort -u | fzf)
    set multi_flag_check 0
end

if set -q _flag_i
    set target_md (gum choose (cat $HOME/.config/ortscripts/pool-history))
end

if set -q _flag_r
    if set -q _flag_t
        set multi_flag_check 1
        set default_pool_array (rg -l "\- $chosen_tag" $default_pool_array)
    end
    set array_length (count $default_pool_array)
    set random_selection (random 1 $array_length)
    set target_md $default_pool_array[$random_selection]
end

if set -q _flag_u
    if set -q _flag_t
        set multi_flag_check 1
        set default_pool_array (rg -l "\- $chosen_tag" $default_pool_array)
    end

    if set -q _flag_r
        set default_pool_array (grep -L "session:" $default_pool_array)
        set array_length (count $default_pool_array)
        set random_selection (random 1 $array_length)
        set target_md $default_pool_array[$random_selection]
    else
        set target_md (grep -L "session:" $default_pool_array | fzf)
    end
end

if test $multi_flag_check -eq 0
    set target_md (rg -l "\- $chosen_tag" $default_pool_array | fzf)
end

if test -z "$target_md"
    set target_md (rg -l "\- pool" $obsidian_folder | fzf)
end

set base_name (basename $target_md)
set history_config "$HOME/.config/ortscripts/pool-history"
sed -i "/$base_name/d" $history_config
sed -i "1i $target_md" $history_config

echo $target_md >/tmp/the-pool.txt

if grep -q 'uuid:' $target_md
    set uuid (grep -oP '(?<=uuid: ).*' $target_md)
else
    set uuid (uuidgen)
    sed -i "/^tags:/i\\uuid: $uuid" "$target_md"
end

if grep -qoP "(?<=session: ).*" $target_md
    set session_number (grep -oP "(?<=session: ).*" $target_md)
    set session_number (math $session_number + 1)
else
    set session_number 1
end
echo "" >>$target_md
echo "# Session $session_number" >>$target_md
echo "" >>$target_md

xset s off
xset -dpms
$script_dir/scripts/pool-record_input.fish $uuid &

obsidian "obsidian://$target_md" >/dev/null 2>&1
set start (date +%s)

echo "From 0-100 describe the value of this subject:"
read user_value
while true
    if string match -qr '^(100|[1-9]?[0-9])$' -- $user_value
        break
    else
        echo "Invalid input, only number 0-100 allowed, try again:"
        read user_value
    end
end

xset s on
xset +dpms
kill $(jobs -p)

set end (date +%s)
set cur_date (date +%d.%m.%y)
set result (math $end - $start)
set result_minutes (math round $result / 60)

if test $user_value -gt 0
    if grep -q 'session:' $target_md
        cp $target_md /tmp/clone.md
        awk '/session/ { 
  for (i = 1; i <= NF; i++) { 
        if ($i ~ /^[0-9]+$/) $i = $i + 1; 
    } 
    } { print }' /tmp/clone.md >$target_md
    else
        sed -i "/^tags:/i\\session: 1" "$target_md"
    end

    if grep -q 'time:' $target_md
        cp $target_md /tmp/clone.md
        awk -v var="$result" '/playtime/ { 
  for (i = 1; i <= NF; i++) { 
        if ($i ~ /^[0-9]+$/) $i = $i + var; 
    } 
    } { print }' /tmp/clone.md >$target_md
    else
        sed -i "/^session:/i\\time: $result" "$target_md"
    end
end

if test $user_value -eq 0
    sed -i "/# Session $session_number/d" "$target_md"
end

if grep -q 'value:' $target_md
    sed -i '/value:/d' "$target_md"
end

sed -i "/^time:/a\\value: $user_value" "$target_md"

set date_last (date +%Y-%m-%d)
if grep -q 'date:' $target_md
    sed -i '/date:/d' "$target_md"
end
sed -i "/^tags:/i\\date: $date_last" "$target_md"
sed -i "/^# Session $session_number/a\\>[!info] Learned: $result_minutes minutes - $cur_date Value: $user_value" "$target_md"

rm /tmp/the-pool.txt
