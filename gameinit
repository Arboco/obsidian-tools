#! /usr/bin/env fish

#argv[1] is obsidian md file title

function help_function
    echo -e "Usage: gameinit [OPTION] \"title of md file with launch property\""
    echo "Options:"
    echo "  -v       Activates media collector on keyboard, specifically for Visual Novels"
    echo "  -x       Activates media collector for Xbox Controller"
end

argparse --name=gameinit v/visualnovel x/xbox h/help m/mambo 'n/name=' -- $argv
or return

set obsidian (ot_config_grab "ObsidianMainFolder")
set session_limit (ot_config_grab "SessionLimit")
set startup_compensator (ot_config_grab "StartupCompensator")
set timeline_option (ot_config_grab "TimelineOption")
set start (date +%s)
set a_path (find $obsidian -type f -name "$argv[1].md" -not -path '*/[@.]*' )
set launch (cat $a_path | grep 'launch:')
set launch (echo $launch | grep -oP '(?<=launch: ).*')
set script_dir (realpath (status dirname))
# turning screen saver and display manager off so they never bother you during gameplay 
xset s off
xset -dpms

if set -q _flag_h
    help_function
    exit
end

if set -q _flag_v
    $script_dir/scripts/record_input.fish $argv[1] 1 &
    $script_dir/scripts/input_idle_watcher.fish 1 (ot_config_grab "Profile1ControllerCheck") &
    echo "Starting Profile 1"
end

if set -q _flag_x
    $script_dir/scripts/record_input.fish $argv[1] 2 &
    $script_dir/scripts/input_idle_watcher.fish 2 (ot_config_grab "Profile2ControllerCheck") &
    echo "Starting Profile 2"
end

if set -q _flag_m
    $script_dir/scripts/record_input.fish $argv[1] 3 &
    $script_dir/scripts/input_idle_watcher.fish 3 (ot_config_grab "Profile3ControllerCheck") &
    echo "Starting Profile 3"
end

if grep "playtime:" $a_path
else
    sed -i "/^launch:/a\\playtime: 0" $a_path
end

echo $a_path >/tmp/obsidian-game.txt

# launching the game, every command after this will only trigger once the game closes
eval $launch; or true
sleep 2

if test (count $argv) -ge 2
    while kill -0 (pidof $argv[2]) 2>/dev/null
        echo "Working..."
        sleep 2
    end
end

if test -f /tmp/idle_counter.txt
    and grep "[0-9]" /tmp/idle_counter.txt
    set second_reduction (cat /tmp/idle_counter.txt)
else
    set second_reduction 0
end

if grep obsidian-tools $a_path
else
    sed -i '/^tags:/a\  - obsidian-tools' $a_path
end

xset s on
xset +dpms

if grep "# Info" $a_path
else
    echo "# Info" >>$a_path
    echo "## Timeline" >>$a_path
end

set end (date +%s)
echo "Time Calculation: "(math $end - $start) " - $second_reduction"
set result (math "($end - $start - $second_reduction) / 60")
set result_minutes (math round $result)
echo "This are approximately $result_minutes minutes"
set result_seconds (math $end - $start - $second_reduction)
if test $result_seconds -gt $startup_compensator
    set result_seconds (math $result_seconds - $startup_compensator)
    echo "Seconds were reduced to $result_seconds"
else
    set result_seconds 0
    echo "Seconds are 0"
end

set cur_date (date +%d.%m.%y)
set cur_hour (date +%H:%M)

if test $timeline_option -eq 1
    if grep "$cur_date" $a_path
        echo "existing date is getting updated"
        set ex_minutes (grep "$cur_date" $a_path | grep -oP '(?<=Played: )..?.?')
        cp $a_path /tmp/clone.md
        awk -v ex="$result_minutes" -v date="$cur_date" '$0 ~ date {
      for (i = 1; i <= NF; i++) { 
        if ($i ~ /^[0-9]+$/) $i = $i + ex;} 
        }
        { print }' /tmp/clone.md >$a_path
    else if test $result_minutes -gt $session_limit
        echo "new date is inserted into timelinie"
        sed "/^## Timeline/{:a; n; /^#/!ba; i\
\\
>[!info] Played: $result_minutes Minutes - $cur_date
}" $a_path >/tmp/clone2.md
        mv /tmp/clone2.md $a_path

        #echo -e "\n>[!info] Played: $result Minutes - $cur_date\n" >>$a_path
    end
end

cp $a_path /tmp/clone.md
awk -v var="$result_seconds" '/playtime/ { 
  for (i = 1; i <= NF; i++) { 
        if ($i ~ /^[0-9]+$/) $i = $i + var; 
    } 
    } { print }' /tmp/clone.md >$a_path

# Makes sure the screenshot script gets killed since it runs as a background process  
kill $(jobs -p)

rm /tmp/clone.md
rm /tmp/idle_counter.txt

echo -e '\a'
